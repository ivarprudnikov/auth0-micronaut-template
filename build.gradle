import com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.4.0"
    id "org.jetbrains.kotlin.kapt" version "1.4.0"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.4.0"
    id "com.github.johnrengelman.shadow" version "6.0.0"
    id "net.ltgt.apt-idea" version "0.21"
    id "application"
}

version "1.0"
group "com.ivarprudnikov.auth0"

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jcenter.bintray.com" }
}

sourceCompatibility = "11"

configurations {
    // for dependencies that are needed for development only
    developmentOnly
}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    // Micronaut main deps
    implementation platform("io.micronaut:micronaut-bom:2.0.1")
    kapt platform("io.micronaut:micronaut-bom:2.0.1")
    implementation "io.micronaut:micronaut-runtime"
    implementation "javax.annotation:javax.annotation-api"
    implementation "io.micronaut.kotlin:micronaut-kotlin-runtime"
    implementation "io.micronaut:micronaut-http-server-netty"
    implementation "io.micronaut:micronaut-http-client"

    // DI
    implementation "io.micronaut:micronaut-inject"
    kapt "io.micronaut:micronaut-inject-java"

    // Security
    implementation "io.micronaut.security:micronaut-security-jwt"
    kapt "io.micronaut.security:micronaut-security"

    // Bean validation
    implementation("io.micronaut:micronaut-validation")
    kapt "io.micronaut:micronaut-validation"

    // Logs
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:2.13.3"
    runtimeOnly "com.amazonaws:aws-lambda-java-log4j2:1.2.0"

    runtimeOnly "com.fasterxml.jackson.module:jackson-module-kotlin"

    // AWS proxy integration
    implementation('io.micronaut.aws:micronaut-function-aws-api-proxy:1.4.1') {
        exclude group: "org.slf4j"
    }

    // Testing
    kaptTest platform("io.micronaut:micronaut-bom:2.0.1")
    kaptTest "io.micronaut:micronaut-inject-java"
    testImplementation enforcedPlatform("io.micronaut:micronaut-bom:2.0.1")
    testImplementation "io.micronaut.test:micronaut-test-kotest:1.2.2"
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:4.2.0"
}

test.classpath += configurations.developmentOnly

mainClassName = "com.ivarprudnikov.auth0.Application"

test {
    useJUnitPlatform()
}

allOpen {
	annotation("io.micronaut.aop.Around")
}

compileKotlin {
	kotlinOptions {
	    jvmTarget = '11'
	    //Will retain parameter names for Java reflection
	    javaParameters = true
	}
}

compileTestKotlin {
	kotlinOptions {
	    jvmTarget = '11'
	    javaParameters = true
	}
}
kapt {
    arguments {
        arg("micronaut.processing.incremental", true)
        arg("micronaut.processing.annotations", "com.ivarprudnikov.auth0.*")
        arg("micronaut.processing.group", "com.ivarprudnikov.auth0")
        arg("micronaut.processing.module", "auth0MicronautTemplate")
    }
}

shadowJar {
    mergeServiceFiles()
    transform(Log4j2PluginsCacheFileTransformer)
}

tasks.withType(JavaExec) {
    classpath += configurations.developmentOnly
    jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
    if (gradle.startParameter.continuous) {
        systemProperties(
                'micronaut.io.watch.restart':'true',
                'micronaut.io.watch.enabled':'true',
                "micronaut.io.watch.paths":"src/main"
        )
    }
}
tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}
